repositories {
    maven { url "https://jitpack.io" }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "kotlin-parcelize"
apply plugin: "kotlin-kapt"

android {
    namespace "mobi.lab.components.ui.shared"

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        compileSdkVersion libs.versions.android.sdk.compile.get()
        targetSdkVersion libs.versions.android.sdk.target.get()
        minSdkVersion libs.versions.android.sdk.min.get()
    }

    lintOptions {
        // Workaround for a bug in Kotlin extension's Parcelize
        warning "ParcelCreator"
    }

    testOptions.unitTests.all {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
            // Always show output even if the result has not changed
            outputs.upToDateWhen { false }
            // Show standard stream outputs if used
            showStandardStreams = true
        }
    }
}

dependencies {
    libsHelper.addUnitTestDependencies(it)
    libsHelper.addInstrumentationTestDependencies(it)

    implementation libs.kotlin
    coreLibraryDesugaring libs.jdk.desugar

    // Util
    implementation libs.androidx.lifecycle.extensions
    implementation libs.timber
    implementation libs.scrolls
    implementation libs.eventbus

    // UI
    implementation libs.androidx.appcompat
    implementation libs.androidx.fragment.ktx
    implementation libs.constraintlayout
    implementation libs.material
    implementation libs.material.progressbar
    implementation libs.material.dialogs
}

def GROUP_CI_BUILD_TASKS = "Build"

task buildAllDebug(dependsOn: ["build"])
buildAllDebug.group = GROUP_CI_BUILD_TASKS
buildAllDebug.description = "Build the debug version"

task buildAllRelease(dependsOn: ["buildAllDebug", "bundleRelease"])
buildAllRelease.group = GROUP_CI_BUILD_TASKS
buildAllRelease.description = "Build the release version"
