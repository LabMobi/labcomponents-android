plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.parcelize
}

project.base.archivesName.set("labcomponents-demo")

android {
    namespace = "mobi.lab.components.demo"

    buildFeatures {
        viewBinding = true
    }

    kotlinOptions {
        jvmTarget = libs.versions.jvmTarget.get()
    }

    compileOptions {
        sourceCompatibility = JavaVersion.valueOf(libs.versions.javaCompatibility.get())
        targetCompatibility = JavaVersion.valueOf(libs.versions.javaCompatibility.get())
    }

    defaultConfig {
        compileSdk = libs.versions.android.sdk.compile.get().toInteger()
        targetSdkVersion libs.versions.android.sdk.target.get()
        minSdkVersion libs.versions.android.sdk.min.get()
        applicationId "mobi.lab.components.demo"

        versionCode 1
        versionName rootProject.ext.libraryVersion

        println "CONF: versionName is set to: " + versionName
        println "CONF: versionCode is set to: " + versionCode
    }

    signingConfigs {
        releaseSigning {
            storeFile file("signing/signing.keystore")
            storePassword "JthvvZuFHcubhcfdNEwlaozMsBwgStijeJuaUIKsBwrNNCHQdn"
            keyAlias "ComponentsDemo"
            keyPassword "XZdAtrAzsIvTLgqPjpfzxpqEnOENBRRpMZOWgsUXyCXSHolTJo"
        }
        debugSigning {
            storeFile file("signing/debug.keystore")
            storePassword "PzIzJAqvONJmhryYnzDgDfFAXMotnJZQhuPCgbFteBYAmNLbwY"
            keyAlias "ComponentsDemo"
            keyPassword "ouHhtZOSoyLaMOKYPJmZsGdZSOAisscmDNFhMpOkpBUpTzzgbe"
        }
    }
    
    buildTypes {
        release {
            // Proguard is enabled by default
            // (Think about the logging lib before turning this off)
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig = signingConfigs.releaseSigning
        }

        debug {
            signingConfig = signingConfigs.debugSigning
        }
    }

    lint {
        // Workaround for a bug in Kotlin extension's Parcelize
        warning "ParcelCreator"

        // In general we want to check for problems. Any time, all the time.
        checkReleaseBuilds = true
        // And fail the build if we have errors
        abortOnError = true

        // Level depends on the type of the job
        /// Fail the build if buildNevercodeRelease target is called and we have missing translations!
        warning("MissingTranslation")
        // Error level things
        /// Hardcoded text should not exist or should have case by case exceptions
        error "HardcodedText"
        // Warning level things
        warning "ImpliedQuantity"
        // Retrofit uses Okio and that generates this warning
        warning "InvalidPackage"
        fatal "StopShip"
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            output.outputFileName = "${base.archivesName.get()}-${variant.name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation project(":lib")

    implementation libs.kotlin
    implementation libs.androidx.navigation.fragment
    implementation libs.androidx.navigation.ui

    // Util
    implementation libs.timber

    // UI
    implementation libs.constraintlayout
    implementation libs.material
    implementation libs.androidx.activity
    implementation libs.androidx.core
}

def GROUP_CI_BUILD_TASKS = "Build"

tasks.register('buildAllDebug') {
    group = GROUP_CI_BUILD_TASKS
    description = "Build the debug version"
    dependsOn "build", ":lib:buildLibDebug"
}

tasks.register('buildAllRelease') {
    group = GROUP_CI_BUILD_TASKS
    description = "Build the release version"
    dependsOn "build", ":lib:buildLibRelease"
}
